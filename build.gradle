plugins {
    id 'java'
    id 'application'
    id 'checkstyle'
}

group = 'org.example'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.testng:testng:6.9.10'
    implementation 'org.seleniumhq.selenium:selenium-java:4.21.0'
}

buildDir = 'build'

test {
    useTestNG() {
        useDefaultListeners = true
        suites "src/test/resources/testng.xml"
    }

    afterSuite { desc, result ->
        if (!desc.parent) {
            println "\nTEST RESULT: ${result.resultType}"
            println "TEST SUMMARY: RAN ${result.testCount} TESTS, " +
                    "${result.successfulTestCount} SUCCEEDED, " +
                    "${result.failedTestCount} FAILED, " +
                    "${result.skippedTestCount} SKIPPED"
        }
    }

    testLogging {
        events "PASSED", "SKIPPED", "FAILED", "STANDARD_OUT", "STANDARD_ERROR"
        exceptionFormat = 'full'
        showStackTraces = true
        showStandardStreams = true
    }

    outputs.upToDateWhen { false }
}

application {
    // âœ… Correct syntax for Gradle 7+
    mainClass = 'demo.App'
}

checkstyle {
    toolVersion = '8.45'
    config = rootProject.resources.text.fromFile('__CRIO__/checkstyle.xml')
    sourceSets = [sourceSets.test]
    ignoreFailures = true
}

tasks.withType(Checkstyle) {
    reports {
        xml.required = true
        html.required = false
    }
}

test {
    dependsOn assemble
    mustRunAfter clean
}
